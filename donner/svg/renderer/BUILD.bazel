load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library")
load("//build_defs:rules.bzl", "donner_perf_sensitive_cc_library")

# Enable CoreText font rendering on macOS. Set with --@donner//donner/svg/renderer:use_coretext=true
bool_flag(
    name = "use_coretext",
    build_setting_default = False,
    visibility = ["//visibility:public"],
)

# Enable Fontconfig font rendering on Linux. Set with --@donner//donner/svg/renderer:use_fontconfig=true
bool_flag(
    name = "use_fontconfig",
    build_setting_default = False,
    visibility = ["//visibility:public"],
)

config_setting(
    name = "macos_coretext",
    constraint_values = ["@platforms//os:macos"],
    flag_values = {":use_coretext": "true"},
)

config_setting(
    name = "linux_fontconfig",
    constraint_values = ["@platforms//os:linux"],
    flag_values = {":use_fontconfig": "true"},
)

cc_library(
    name = "renderer_image_io",
    srcs = ["RendererImageIO.cc"],
    hdrs = ["RendererImageIO.h"],
    visibility = ["//donner/svg/renderer:__subpackages__"],
    deps = [
        "@stb//:image_write",
    ],
)

cc_library(
    name = "renderer_utils",
    srcs = ["RendererUtils.cc"],
    hdrs = ["RendererUtils.h"],
    visibility = ["//donner/svg:__subpackages__"],
    deps = [
        "//donner/svg",
    ],
)

cc_library(
    name = "rendering_context",
    srcs = ["RenderingContext.cc"],
    hdrs = ["RenderingContext.h"],
    visibility = ["//donner/svg:__subpackages__"],
    deps = [
        "//donner/svg/components",
    ],
)

donner_perf_sensitive_cc_library(
    name = "skia_deps",
    defines = select({
        ":macos_coretext": ["DONNER_USE_CORETEXT"],
        ":linux_fontconfig": ["DONNER_USE_FREETYPE_WITH_FONTCONFIG"],
        "//conditions:default": ["DONNER_USE_FREETYPE"],
    }),
    linkopts = select({
        ":linux_fontconfig": ["-lfontconfig"],
        "//conditions:default": [],
    }),
    deps = [
        "@skia//:core",
        "@skia//:pathops",
    ] + select({
        ":macos_coretext": [
            "@skia//:fontmgr_coretext",
        ],
        ":linux_fontconfig": [
            "@skia//:fontmgr_fontconfig_freetype",
        ],
        "//conditions:default": [
            "@skia//:fontmgr_empty_freetype",
        ],
    }),
)

cc_library(
    name = "renderer_skia",
    srcs = ["RendererSkia.cc"],
    hdrs = ["RendererSkia.h"],
    deps = [
        ":renderer_image_io",
        ":renderer_utils",
        ":skia_deps",
        "//donner/base/fonts",
        "//donner/svg",
        "//donner/svg/core",
        "//donner/svg/renderer/common",
        "//third_party/public-sans",
    ],
)

cc_library(
    name = "renderer",
    visibility = ["//visibility:public"],
    deps = [":renderer_skia"],
)

cc_binary(
    name = "renderer_tool",
    srcs = ["renderer_tool.cc"],
    deps = [
        ":renderer_skia",
        "//donner/svg/parser",
        "@com_google_absl//absl/debugging:failure_signal_handler",
        "@com_google_absl//absl/debugging:symbolize",
    ],
)
